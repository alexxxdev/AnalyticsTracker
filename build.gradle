buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'

group 'com.github.alexxxdev.analyticsTracker'
version '1.0.1'

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()

    implementation 'com.squareup:kotlinpoet:0.6.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Analytics Tracker',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion,
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mavenLocal().url)
            pom.groupId = rootProject.group
            pom.artifactId = project.name
            pom.version = rootProject.version
            pom.project{
                name 'AnalyticsTracker'
                description ' '
                url 'https://github.com/alexxxdev/AnalyticsTracker'
                inceptionYear '2017'

                developers {
                    developer {
                        id 'alexxxdev'
                        name 'Alexey Derendyaev'
                        email 'alexxxdev@yandex.ru'
                    }
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}